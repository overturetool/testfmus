class Controller

instance variables

-- output of FMU
output: RealPort := new RealPort(0.0);

-- thread period
private period: nat := 1E7;

private amplitude: real := 1.0;
private omega: real := 1.0;

values

operations

	-- constructor for Controller
	public Controller: RealPort ==> Controller
	Controller(out) == (
		output := out;
		period := frequency_to_period(System`hwi.frequency.getValue());
		amplitude := System`hwi.amplitude.getValue();
		omega := System`hwi.omega.getValue();
 	);

	-- main loop
	public Step: () ==> ()
	Step() == (
		let a1 = amplitude * omega / MATH`pi,     
				t1 = fmod(time/1e9, 2 * MATH`pi / omega)
		in
			if t1 < MATH`pi / omega then output.setValue(a1 * t1)
			else output.setValue(-a1 * t1+2 * amplitude)
);
	  
thread periodic(period,0,0,0)(Step)	  

functions 

-- convert frequency to period in nanoseconds
private frequency_to_period: real -> nat
frequency_to_period(f) == floor 1E9/f;

private fmod: real * real -> real
fmod(a,b) == 
	a - floor(a / b) * b
	  
end Controller